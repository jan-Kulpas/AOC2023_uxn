( Advent of Code: Day 1 - first star )

|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0000

	@first-num $1
	@last-num $1

|0100

@on-reset ( -> )
	( set filepath adress ) ;filepath .File/name DEO2
	( set read length for dir listing ) #0004 .File/length DEO2
	( read dir listing ) ;input .File/stat DEO2
	( | convert file size to a number and set read length )
	[ ;input shex ] .File/length DEO2
	( read file ) ;input .File/read DEO2
	;input [ LIT2r 0000 ]
	&loop ( -- )
		<process-line>
		[ LITr 00 ] STH
		ADD2r LDAk ?&loop
	POP2
	( | print result )
	{ "result: 20 00 }
	STH2r <pstr>
	STH2r <pdec>
	LIT2 0a -Console/write DEO
	#80 .System/state DEO
	BRK

(
@|functions )

@<process-line> ( str* -- [ str+line.len ]* val )
	#ff .first-num STZ
	&w ( -- )
		( | check if number )
		LDAk is-num ?{ !&not-number }
		LDAk #30 SUB .first-num LDZ #ff NEQ ?{ DUP .first-num STZ }
		.last-num STZ
		( | loop if not end line )
		&not-number INC2 LDAk #0a EQU ?{ LDAk ?&w }
	INC2
	( | combine numbers for result )
	.first-num LDZ #0a MUL .last-num LDZ ADD JMP2r

(
@|helpers )

@is-num ( c -- bool )
	DUP #29 GTH SWP #40 LTH AND JMP2r

@pstack ( -- )
	#01 .System/debug DEO
	JMP2r

@pline ( str* -- [ str+line.len ]* )
	&w ( -- )
		LDAk #18 DEO
		INC2 LDAk #0a EQU ?{ LDAk ?&w }
	LDAk #18 DEO
	INC2 JMP2r

@<pstr> ( str* -- )
	&w ( -- )
		LDAk #18 DEO
		INC2 LDAk ?&w
	POP2 JMP2r

@phex ( short* -- )
	SWP phex/b
	&b ( -- )
		DUP #04 SFT phex/c
	&c ( -- )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r

@<pdec> ( short* -- )
	#2710 [ LIT2r 00fb ]
	&w ( -- )
		DIV2k #000a DIV2k MUL2 SUB2 SWPr EQUk OVR STHkr EQU AND ?{
			DUP [ LIT "0 ] ADD #19 DEO
			INCr }
		POP2 #000a DIV2 SWPr INCr STHkr ?&w
	POP2r POP2 POP2 JMP2r

@pbool ( bool -- )
	?{ #30 !&print }
	#31 &print #18 DEO
	JMP2r

@shex ( str* -- val* )
	[ LIT2r 0000 ]
	&w ( -- )
		( validate ) LDAk chex INC #00 EQU ?&end
		( accumulate ) [ LITr 40 ] SFT2r
		( combine ) LDAk chex [ LITr 00 ] STH
		ADD2r
		( continue ) INC2 LDAk ?&w
	&end POP2 STH2r JMP2r

@chex ( c -- val? )
	( dec ) [ LIT "0 ] SUB DUP #09 GTH ?{ JMP2r }
	( hex ) #27 SUB DUP #0f GTH ?{ JMP2r }
	( err ) POP #ff JMP2r

(
@|memory )

@filepath
	"./uxn_aoc/day1/input.txt $1

@input $8000

